apply from: "$rootDir/gradle/scalariform.gradle"
apply plugin: 'scalaStyle'
apply plugin: 'org.scoverage'
apply plugin: 'com.github.ben-manes.versions'

scalaStyle {
    configLocation = "$rootDir/gradle/scalastyle_config.xml"
    includeTestSourceDirectory = true
    failOnViolation = true
    failOnWarning = false
    source = "src/main/scala"
    testSource = "src/test/scala"
}
scalaStyle.dependsOn('compileScala', 'compileTestScala')
check.dependsOn('scalaStyle')
test.shouldRunAfter('scalaStyle')

checkScoverage {
    minimumRate = 0.90
}
check.dependsOn('checkScoverage')
checkScoverage.shouldRunAfter('test')

dependencyUpdates {
    revision="release"
    resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['snap', 'alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}
